#compdef aws

# AWS-supplied completion seems to be bash only
# (documentation includes zsh installation which however merely invokes
# the 'complete' function from bashcompinit)

cfgfile=$HOME/.aws/config

_s3_find_endpoint() {
  if [[ -n $profile && -f $cfgfile ]] ; then
    # note caret escape
    grep \^"\[profile $profile\]" -A15 $cfgfile | \
      grep \^endpoint -m1 | \
      cut -d= -f2 | awk '{print $1}' | \
      sed 's/:/\\:/g'
  fi
}

_s3_endpoint() {
  extracted=$(_s3_find_endpoint)
  # or: _describe -t endpoint Endpoint extracted
  [[ -n $extracted ]] && _values Endpoint $extracted
}

_s3_buckets() {
  if [[ -n $profile && -f $cfgfile ]] ; then
    buckets=($(grep \^"\[profile $profile\]" -A15 $cfgfile | \
      grep \^buckets -m1 | \
      cut -d= -f2 | awk '{print $1}' | \
      tr , ' '))
    compadd -qS '' "${buckets[@]}"
  fi
}

_s3_remote() {
  if compset -P 's3://' ; then
    _alternative 'bucket::_s3_buckets'
  fi
}

_s3_local_or_remote() {
  _alternative \
    'remote::_s3_remote' \
    'local::_files' \
    'dash::(-)' \
    && return
  return 1
}

_aws_profile() {
  profiles=($(grep \^'\[profile' $cfgfile | tr -d '[]' | awk '{print $2}'))
  compadd ${profiles[@]}
}

_aws-s3() {
  _arguments \
    --endpoint+:Endpoint:_s3_endpoint \
    ':command:(configure ls rm cp sync)' \
    '*:file:_s3_local_or_remote' \
    && return
  return 1
}

_aws() {
  if [[ $service == aws ]]; then
    local curcontext=$curcontext state
    local -A opt_args
    _arguments -C \
      '--profile+:Profile:_aws_profile' \
      '(-): :->command' \
      '(-)*:: :->option-or-argument' && return

    # don't know how to retain opt_args and avoid this global variable
    profile=${opt_args[--profile]:-}

    case $state in
      (command)
        compadd s3
        return
        ;;
      (option-or-argument)
        curcontext=${curcontext%:*:*}:aws-$words[1]:
        integer ret=1
	if ! _call_function ret _aws-$words[1]; then
	  if [[ $words[1] = \!* ]]; then
	    words[1]=${words[1]##\!}
	    _normal && return
	  elif zstyle -T :completion:$curcontext: use-fallback; then
	    _default && return
	  else
	    _message "unknown sub-command: $words[1]"
	  fi
        fi
        return ret
        ;;
    esac
  fi
}

_aws
